plugins {
    id 'java'
    id 'io.freefair.lombok' version '6.6.1'
    id 'org.springframework.boot' version '2.6.14'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
    id 'maven-publish'
}

ext {
    springCloudVersion = '2021.0.5'
    openFeignVersion = '11.7'
}

if (!project.hasProperty('gitBranch')) {
    ext.gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

allprojects {
    group = 'com.iexec.sms'
    if (gitBranch != 'main' && gitBranch != 'master' && !(gitBranch ==~ '(release|hotfix|support)/.*')) {
        version += '-NEXT-SNAPSHOT'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://docker-regis-adm.iex.ec/repository/maven-public/'
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        maven {
            url 'https://nexus.intra.iex.ec/repository/maven-public/'
        }
        maven {
            url 'https://jitpack.io'
        }
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // iexec
    implementation "com.iexec.common:iexec-common:$iexecCommonVersion"
    implementation project(':iexec-sms-library')

    // spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // H2
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2:1.4.200'

    // Spring Doc
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.3'

    //ssl
    implementation 'org.apache.httpcomponents:httpclient'

    // Web3j issues, see core build.gradle
    // NoSuchMethodError: 'okhttp3.RequestBody okhttp3.RequestBody.create(java.lang.String, okhttp3.MediaType)'
    // Spring Boot dependencies BOM enforces okhttp3 3.14.9 in 2.6.X
    // It is required to define the dependency version required by web3j until migration to at least Spring Boot 2.7.X
    implementation 'com.squareup.okhttp3:okhttp:4.9.0' // Web3j issue: https://github.com/web3j/web3j/issues/1180

    // velocity for templating
    implementation 'org.apache.velocity:velocity-engine-core:2.0'

    // MongoDB
    //implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    //implementation 'org.springframework.boot:spring-boot-starter'
    //testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // test
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    // testImplementation 'org.mockito:mockito-inline:2.13.0' // activates mocking final classes/methods

    // test containers
    testImplementation 'org.testcontainers:junit-jupiter:1.16.0'
    testImplementation 'org.testcontainers:testcontainers:1.16.0'
    testImplementation 'org.testcontainers:mongodb:1.16.0'

    // feign
    implementation "io.github.openfeign:feign-core:$openFeignVersion"
    implementation "io.github.openfeign:feign-jackson:$openFeignVersion"
    implementation "io.github.openfeign:feign-slf4j:$openFeignVersion"
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            srcDir 'src/itest/java'
        }
        resources.srcDir 'src/itest/resources'
    }
}

springBoot {
    buildInfo()
}

test {
    useJUnitPlatform()
}

task itest(type:Test) {
    group       'Verification'
    description 'Runs the integration tests.'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false } // run always
    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.7'
}
// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.required = true
    }
}
tasks.test.finalizedBy    tasks.jacocoTestReport
tasks.sonarqube.dependsOn tasks.jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            artifact bootJar
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url project.hasProperty('nexusUrl') ? nexusUrl : ''
        }
    }
}

ext.jarPathForOCI  = relativePath(tasks.bootJar.outputs.files.singleFile)
ext.gitShortCommit = 'git rev-parse --short=8 HEAD'.execute().text.trim()
ext.ociImageName   = 'local/' + ['bash', '-c', 'basename $(git config --get remote.origin.url) .git'].execute().text.trim()

task buildImage(type: Exec) {
    group       'Build'
    description 'Builds an OCI image from a Dockerfile.'
    dependsOn   bootJar
    commandLine ('sh', '-c', "docker build --build-arg jar=$jarPathForOCI"
            + " -t $ociImageName:$gitShortCommit . && docker tag $ociImageName:$gitShortCommit $ociImageName:dev")
    standardOutput = new ByteArrayOutputStream()

    ext.output = {
        println standardOutput
        return standardOutput.toString()
    }
}

task buildSconeImage(type: Exec) {
    group       "Build"
    description "Build an OCI image compatible with scontain TEE framework"
    dependsOn   buildImage
    commandLine "docker/sconify.sh"
    environment "IMG_FROM", "$ociImageName:dev"
    environment "IMG_TO", "$ociImageName-unlocked:dev"
}
