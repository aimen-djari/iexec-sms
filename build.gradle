import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven'
    id 'jacoco'
    id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'com.iexec.sms'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://nexus.iex.ec/repository/maven-public/"
    }
}

configurations {
    deployerJars
}

ext {
    springCloudVersion = '2020.0.1'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // spring
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.cloud:spring-cloud-starter-openfeign"
    compile "org.springframework.retry:spring-retry"
    compile "org.springframework.cloud:spring-cloud-starter-openfeign"

    // test
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    // testImplementation 'org.mockito:mockito-inline:2.13.0' // activates mocking final classes/methods

    //ssl
    compile 'org.apache.httpcomponents:httpclient:4.5.9'

    // iexec
    compile "com.iexec.common:iexec-common:$iexecCommonVersion"
    // compile files("../iexec-common/build/libs/iexec-common-${iexecCommonVersion}.jar")

    // Web3j issues, see core build.gradle
    implementation 'com.squareup.okhttp3:okhttp:4.3.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.50'

    // swagger
    compile "io.springfox:springfox-swagger2:2.9.2"
    compile "io.springfox:springfox-swagger-ui:2.9.2"

    // velocity for templating
    compile "org.apache.velocity:velocity:1.7"

    // lombok
    compileOnly "org.projectlombok:lombok:1.18.2"
    annotationProcessor "org.projectlombok:lombok:1.18.2"

    // H2
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtime 'com.h2database:h2:1.4.200'

    // MongoDB
    //compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    //implementation 'org.springframework.boot:spring-boot-starter'
    //testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.3"
}
build.dependsOn jacocoTestReport

def gitBranch = 'git name-rev --name-only HEAD'.execute().text.trim()
def isMasterBranch = gitBranch == "master"
def isDevelopBranch = gitBranch == "develop"
def canUploadArchives = (isMasterBranch || isDevelopBranch ) && project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")
def gitShortCommit = 'git rev-parse --short HEAD'.execute().text.trim()
def isSnapshotVersion = project.version.contains("SNAPSHOT")

project.ext.getNexusMaven = {
    def nexusMavenBase = "https://nexus.iex.ec/repository"
    if (isSnapshotVersion) {
        return nexusMavenBase + "/maven-snapshots/"
    } else {
        return nexusMavenBase + "/maven-releases/"
    }
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: getNexusMaven()) {
            authentication(userName: project.nexusUser, password: project.nexusPassword)
        }
    }
}
uploadArchives.enabled = canUploadArchives

// create the version controller for the core
task createVersion(type: Copy) {
    // delete old one
    delete 'src/main/java/com/iexec/sms/utils/version/Version.java'
    // use and copy template to the new location
    from 'src/main/resources/Version.java.template'
    into 'src/main/java/com/iexec/sms/utils/version/'

    rename { String fileName ->
        fileName.replace('.template', '')
    }
    // replace tokens in the template file
    filter(ReplaceTokens, tokens: [projectversion: "${version}".toString()])
}
// the createVersion task should be called before compileJava or the version service will not work
compileJava.dependsOn createVersion

def imageName = "nexus.iex.ec/iexec-sms"
def trustedDockerfileName = "Dockerfile"
def untrustedDockerfileName = "Dockerfile-untrusted"

project.ext.getDockerImageNameFull = {
    def imageNameWithVersion = imageName + ":${version}"
    if (isSnapshotVersion) {
        return imageNameWithVersion + "-" + gitShortCommit
    } else {
        return imageNameWithVersion
    }
}

project.ext.getDockerImageNameShortCommit = {
    return imageName + ":" + gitShortCommit
}

task prepareDockerFile(type: Copy) {
    from 'src/main/resources/'
    into 'build/resources/main/'
    filter(ReplaceTokens, tokens: [projectversion: "${version}".toString()])
}

task buildImage(type: Exec) {
    description 'Building iexec-sms Docker image'
    commandLine("sh", "-c", "docker image build -f build/resources/main/" + untrustedDockerfileName + " -t " + getDockerImageNameFull() + " . &&" +  // scone
            "docker tag " + getDockerImageNameFull() + " " + imageName + ":dev")
    standardOutput = new ByteArrayOutputStream()

    ext.output = {
        println standardOutput
        return standardOutput.toString()
    }
}
buildImage.dependsOn prepareDockerFile

task templatePalaemon {
    String smsMrEnclave
    String smsFspfKey
    String smsFspfTag

    /*
    * Docker build will produce such logs: "RUN .. <MRENCLAVE>$MRENCLAVE</MRENCLAVE> ... <MRENCLAVE>abcdef</MRENCLAVE>"
    * We need to extract the 2nd occurence 'abcdef'
    */
    ext.extractValueBetweenTags = { s, tag ->
        String beginTag = "<" + tag + ">"
        String endTag = "</" + tag + ">"
        return s.substring(s.indexOf(beginTag, s.indexOf(beginTag) + 2) + beginTag.length(), s.indexOf(endTag, s.indexOf(endTag) + 2))
    }

    doFirst {
        String logs = buildImage.output()
        smsMrEnclave = extractValueBetweenTags(logs, "MRENCLAVE")
        smsFspfKey = extractValueBetweenTags(logs, "FSPF_KEY")
        smsFspfTag = extractValueBetweenTags(logs, "FSPF_TAG")
    }

    doLast{
        copy {
            // delete old one
            delete 'src/main/resources/boot/sms-palaemon-conf.yml'
            // use and copy template to the new location
            from 'src/main/resources/sms-palaemon-conf.yml.template'
            into 'src/main/resources/boot/'

            rename { String fileName ->
                fileName.replace('.template', '')
            }
            // replace tokens in the template file
            filter(ReplaceTokens, tokens: [
                    IEXEC_SMS_PALAEMON_SERVICE_NAME: "${rootProject.name}".toString(),
                    IEXEC_SMS_MRENCLAVE: smsMrEnclave,
                    IEXEC_SMS_FSPF_KEY: smsFspfKey,
                    IEXEC_SMS_FSPF_TAG: smsFspfTag,
            ])

        }
    }
}
//templatePalaemon.dependsOn buildImage
//buildImage.finalizedBy templatePalaemon

task pushImage(type: Exec) {
    if (project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
        commandLine("sh", "-c", "docker login -u " + project.nexusUser + " -p " + project.nexusPassword + " nexus.iex.ec && " +
                "docker push " + getDockerImageNameFull() + " && " +
                "docker tag " + getDockerImageNameFull() + " " + getDockerImageNameShortCommit() + " && " +
                "docker push " + getDockerImageNameShortCommit() + " && " +
                "docker logout")
    } else {
        println "Credentials for DockerHub are missing, the images cannot be pushed"
    }
}
pushImage.dependsOn buildImage
pushImage.enabled = canUploadArchives
